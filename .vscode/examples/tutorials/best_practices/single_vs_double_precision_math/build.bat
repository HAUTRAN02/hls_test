:: This batch file will compile the example design:
::   1) default      Compile the example design
::   2) clean        Remove any temporary files generated by the compiler
:: Usage: build.bat <target>
:: Example: build.bat default

@ECHO OFF

:: Only 2 arguments expected
if not "%3"=="" goto usage

set "TARGET=default"
if not "%1"=="" set "TARGET=%1"

set "DEVICE=Arria10"
if not "%2"=="" set "DEVICE=%2"

if "%TARGET%" == "default" (
  echo i++ -march=%DEVICE% tutorial.cpp -o tutorial-sp.exe
  i++ -march=%DEVICE% tutorial.cpp -o tutorial-sp.exe
  if not ERRORLEVEL 0 (
    echo Error: Compile failed
    exit /b 1
  )

  echo i++ -march=%DEVICE% -DUSE_DOUBLE_PRECISION tutorial.cpp -o tutorial-dp.exe
  i++ -march=%DEVICE% -DUSE_DOUBLE_PRECISION tutorial.cpp -o tutorial-dp.exe
  if not ERRORLEVEL 0 (
    echo Error: Compile failed
    exit /b 1
  )

  echo tutorial-sp.exe
  tutorial-sp.exe
  echo.
  echo tutorial-dp.exe
  tutorial-dp.exe

  echo.
  echo Compilation complete.  Open the README and example the report files to examine the impact of double vs. single precision operations.
  goto:eof

) else if "%TARGET%" == "clean" (
  del /s /f *.tmp
  del /s /f *.exe
  del /s /f *.obj
  rmdir /s /q tutorial-sp.prj 2>nul
  rmdir /s /q tutorial-dp.prj 2>nul
  goto:eof
) else (
  goto usage
)

:: Dump the usage if we get unexpected input
:usage
echo Usage: build.bat [target]
echo Targets: default, clean
echo Example: build.bat default
exit /b 2
