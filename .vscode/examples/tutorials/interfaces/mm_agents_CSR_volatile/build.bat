:: This batch file will compile the example design:
::   1) default      Compile the example design
::   2) clean        Remove any temporary files generated by the compiler
:: Usage: build.bat <target>
:: Example: build.bat default

@ECHO OFF
:: Only 2 arguments expected
if not "%3"=="" goto usage

set "TARGET=default"
if not "%1"=="" set "TARGET=%1"

set "DEVICE=Arria10"
if not "%2"=="" set "DEVICE=%2"

set DESIGNS=part_1_enqueue_nonvolatile part_2_enqueue_volatile

if "%TARGET%" == "default" (

  for %%a in (%DESIGNS%) do (
    echo i++ -march=%DEVICE% %%a.cpp -o %%a.exe
    i++ -march=%DEVICE% -ghdl %%a.cpp -o %%a.exe
    if not ERRORLEVEL 0 (
      echo Error: Compile failed
      exit /b 1
    )
  )

  echo i++ -march=%DEVICE% part_3_CSR_nonvolatile.cpp -o part_3_CSR_nonvolatile.exe 
  i++ -march=%DEVICE% part_3_CSR_volatile.cpp -o part_3_CSR_volatile.exe --simulator none

  for %%a in (%DESIGNS%) do (
    echo %%a.exe	
    %%a.exe
  )

  ip-make-ipx --thorough-descent > ip-make-ipx.log 2>&1
  qsys-script --script=top.tcl --quartus-project=none --cmd="set device_family {%DEVICE%}" > qsys-script.log 2>&1
  qsys-generate top.qsys --simulation=VERILOG > qsys-generate.log 2>&1
  mkdir simulations 2>nul
  vsim -batch -do part_1_test.tcl > vsim1.log 2>&1
  vsim -batch -do part_2_test.tcl > vsim2.log 2>&1
  vsim -batch -do part_3_test.tcl > vsim3.log 2>&1

  goto:eof

) else if "%TARGET%" == "clean" (
  del /s /f *.tmp 2>nul
  del /s /f *.obj 2>nul
  del /s /f top.qsys 2>nul
  del /s /f components.ipx 2>nul
  for %%a in (%DESIGNS%) do (
    rmdir /s /q %%a.prj 2>nul
  )
  rmdir /s /q part_3_CSR_nonvolatile.prj 2>nul
  rmdir /s /q top 2>nul
  goto:eof
) else (
  goto usage
)

:: Dump the usage if we get unexpected input
:usage
echo Usage: build.bat [target]
echo Targets: default, clean
echo Example: build.bat default
exit /b 2
