:: This batch file will compile the example design:
::   1) default      Compile the example design
::   2) clean        Remove any temporary files generated by the compiler
:: Usage: build.bat <target>
:: Example: build.bat default

@ECHO OFF
:: Only 2 arguments expected
if not "%3"=="" goto usage

set "TARGET=default"
if not "%1"=="" set "TARGET=%1"

set "DEVICE=Arria10"
if not "%2"=="" set "DEVICE=%2"

set TARGETS=part2 part3
set SIM_TARGETS= part1

if "%TARGET%" == "default" (

  for %%a in (%TARGETS%) do (
      echo i++ -march=%DEVICE% --simulator none %%a.cpp -o %%a
      i++ -march=%DEVICE% --simulator none %%a.cpp -o %%a
      if not ERRORLEVEL 0 (
          echo Error: Compile failed
          exit /b 1
      )
  )

  ip-make-ipx --thorough-descent > ip-make-ipx.log 2>&1
  qsys-script --script=top.tcl --quartus-project=none --cmd="set device_family {%DEVICE%}" > qsys-script.log 2>&1
  qsys-generate top.qsys --simulation=VERILOG > qsys-generate.log 2>&1
  mkdir simulations 2>nul
  vsim -batch -do part2_test.tcl > vsim2.log 2>&1
  vsim -batch -do part3_test.tcl > vsim3.log 2>&1

  for %%a in (%SIM_TARGETS%) do (
    echo i++ -march=%DEVICE% -ghdl %%a.cpp -o %%a.exe
    i++ -march=%DEVICE% -ghdl %%a.cpp -o %%a.exe
    if not ERRORLEVEL 0 (
      echo Error: Compile failed
      exit /b 1
    )
  )

  for %%a in (%SIM_TARGETS%) do (
      echo %%a.exe	
      %%a.exe
  )
  goto:eof

) else if "%TARGET%" == "clean" (
  del /s /f *.tmp 2>nul
  del /s /f *.obj *.iipx *.log 2>nul
  del /s /f top.qsys 2>nul
  del /s /f components.ipx 2>nul
  for %%a in (%SIM_TARGETS%) do (
    rmdir /s /q %%a.prj 2>nul
    del %%a.exe
  )
  for %%a in (%TARGETS%) do (
    rmdir /s /q %%a.prj 2>nul
  )
  rmdir /s /q top work 2>nul
  goto:eof
) else (
  goto usage
)

:: Dump the usage if we get unexpected input
:usage
echo Usage: build.bat [target]
echo Targets: default, clean
echo Example: build.bat default
exit /b 2
