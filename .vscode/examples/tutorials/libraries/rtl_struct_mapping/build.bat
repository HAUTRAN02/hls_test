:: This batch file will compile the tutorial design:
::   1) default      Compile the tutorial design
::   2) clean        Remove any temporary files generated by the compiler
:: Usage: build.bat <target>
:: Example: build.bat default

@ECHO OFF

:: Only 2 arguments expected
if not "%3"=="" goto usage

set "TARGET=default"
if not "%1"=="" set "TARGET=%1"

set "DEVICE=Arria10"
if not "%2"=="" set "DEVICE=%2"

set DESIGNS=rtl_struct_packer_test

if "%TARGET%" == "default" (
  echo i++ -march=%DEVICE% --simulator none rtl_struct_packer_emu_models.cpp -DEXTRACT_INTERFACE_STRUCT
  i++ -march=%DEVICE% --simulator none rtl_struct_packer_emu_models.cpp -DEXTRACT_INTERFACE_STRUCT
  copy a.prj\components\rtl_struct_a_packer\interface_structs.v interface_structs.sv 2>nul
  rmdir /s /q a.prj 2>nul
  echo fpga_crossgen --target hls --emulation_model rtl_struct_packer_emu_models.cpp rtl_struct_packer.xml
  fpga_crossgen --target hls --emulation_model rtl_struct_packer_emu_models.cpp rtl_struct_packer.xml
  echo fpga_libtool --target hls --create rtl.lib  rtl_struct_packer.obj
  fpga_libtool --target hls --create rtl.lib  rtl_struct_packer.obj
  for %%a in (%DESIGNS%) do (
    echo i++  -march=%DEVICE% -ghdl rtl.lib %%a.cpp -o %%a.exe
    i++  -march=%DEVICE% -ghdl rtl.lib %%a.cpp -o %%a.exe
    if not ERRORLEVEL 0 (
      echo Error: Compile failed
      exit /b 1
    )
  )

  for %%a in (%DESIGNS%) do (
    echo %%a.exe
    %%a.exe
  )

  goto:eof

) else if "%TARGET%" == "clean" (
  del /s /f *.tmp 2>nul
  del /s /f *.exe 2>nul
  del /s /f *.lib 2>nul
  del /s /f *.obj 2>nul
  del /s /f interface_structs.sv
  rmdir /s /q *.prj 2>nul
  goto:eof
) else (
  goto usage
)

:: Dump the usage if we get unexpected input
:usage
echo Usage: build.bat [target]
echo Targets: default, clean
echo Example: build.bat default
exit /b 2
