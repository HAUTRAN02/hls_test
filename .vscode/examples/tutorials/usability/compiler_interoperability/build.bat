:: This batch file will compile the example design:
::   1) default      Compile the example design
::   2) clean        Remove any temporary files generated by the compiler
:: Usage: build.bat <target>
:: Example: build.bat default

@ECHO OFF
:: Only 2 arguments expected
if not "%3"=="" goto usage

:: Get Path to our includes. Our compiler knows where it is, CL doesn't
for /f "delims=" %%I in ('where i++') do (
  if "x%%I" == "x" (
    echo Error: Cannot find i++
	exit /b 1
  )
  set "HLS_INCLUDE=%%~dpI..\include"
  goto done
)
:done

set "TARGET=default"
if not "%1"=="" set "TARGET=%1"

set "DEVICE=Arria10"
if not "%2"=="" set "DEVICE=%2"

if "%TARGET%" == "default" (

  echo 1. Compile your component code into an object file using i++ 
  echo i++ -c -march=%DEVICE% -c accelerate.cpp -o component_fpga.obj
  i++ -c -march=%DEVICE% accelerate.cpp -o component_fpga.obj
  if not ERRORLEVEL 0 (
    echo Error: Compile failed
    exit /b 1
  )

  echo 2. Compile your testbench code into an object file using i++ or your native compiler
  echo i++ -c -march=x86-64 testbench.cpp -o testbench-hls.obj
  i++ -c -march=x86-64 testbench.cpp -o testbench-hls.obj
  if not ERRORLEVEL 0 (
    echo Error: Compile failed
    exit /b 1
  )

  echo CL /std:c++17 /nologo /MD /EHsc /c /I%HLS_INCLUDE% testbench.cpp /Fotestbench-native.obj
  CL /std:c++17 /nologo /MD /EHsc /c /I%HLS_INCLUDE% testbench.cpp /Fotestbench-native.obj >msvc_cl.log
  if not ERRORLEVEL 0 (
    echo Error: Compile failed
    exit /b 1
  )

  echo 3. Link the object files using i++.
  echo i++ component_fpga.obj testbench-hls.obj -o testbench_exe_hls.exe
  i++ component_fpga.obj testbench-hls.obj -o testbench_exe_hls.exe
  if not ERRORLEVEL 0 (
    echo Error: Compile failed
    exit /b 1
  )

  echo i++ component_fpga.obj testbench-native.obj -o testbench_exe_native.exe
  i++ component_fpga.obj testbench-native.obj -o testbench_exe_native.exe
  if not ERRORLEVEL 0 (
    echo Error: Compile failed
    exit /b 1
  )

  echo testbench_exe_hls.exe
  testbench_exe_hls.exe

  echo testbench_exe_native.exe
  testbench_exe_native.exe

  goto:eof

) else if "%TARGET%" == "clean" (
  del /s /f *.tmp
  del /s /f *.exe
  del /s /f *.obj
  del /s /f *.pdb
  del /s /f *.log
  rmdir /s /q component_fpga.prj 2>nul
  goto:eof
) else (
  goto usage
)

:: Dump the usage if we get unexpected input
:usage
echo Usage: build.bat [target]
echo Targets: default, clean
echo Example: build.bat default
exit /b 2
